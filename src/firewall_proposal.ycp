/**
 * File:	clients/firewall_proposal.ycp
 * Package:	Firewall configuration
 * Summary:	Firewall configuration proposal
 * Authors:	Lukas Ocilka <locilka@suse.cz>
 *
 * $Id$
 */

{

textdomain "firewall";

/* The main () */
y2milestone("----------------------------------------");
y2milestone("Firewall proposal started");
y2milestone("Arguments: %1", WFM::Args());

import "SuSEFirewall";
import "Popup";
import "Progress";

SuSEFirewall::SetModified();

string func = (string) WFM::Args(0);
map param = (map) WFM::Args(1);
map ret = $[];

/* create a textual proposal */
if(func == "MakeProposal") {
    boolean force_reset = param["force_reset"]:false;

    if (force_reset || !SuSEFirewall::proposal_changed_by_user) {
	Progress::off();
	SuSEFirewall::Reset();
    } else if (SuSEFirewall::proposal_changed_by_user) {
	// proposal was changed by user though but there are still some things to add maybe
	SuSEFirewall::UpdateProposal();
    }
    SuSEFirewall::start = SuSEFirewall::enable_firewall;

    string warning = nil;
    symbol warning_level = nil;
    map<string, string> proposal = (map<string, string>) SuSEFirewall::GetPreformattedProposal ();
    Progress::on();

    ret = $[
	"preformatted_proposal" : proposal["out"]:"",
	"warning_level" : (proposal["warning"]:"" == "" ? nil : `warning),
	"warning" : proposal["warning"]:nil,
	"links" : [
	    "firewall--enable_firewall_in_proposal", "firewall--disable_firewall_in_proposal",
	    "firewall--enable_ssh_in_proposal", "firewall--disable_ssh_in_proposal",
	],
    ];
}
/* run the module */
else if(func == "AskUser") {
    any chosen_id = param["chosen_id"]:nil;
    y2milestone("Firewall Proposal wanted to change with id %1", chosen_id);

    // user clicked on any clickable <a href> in proposal
    if (chosen_id == "firewall--enable_firewall_in_proposal") {
	y2milestone("Firewall enabled by single-click");
	SuSEFirewall::enable_firewall = true;
	ret = $[ "workflow_sequence" : `next ];
    }
    else if (chosen_id == "firewall--disable_firewall_in_proposal") {
	y2milestone("Firewall disabled by single-click");
	SuSEFirewall::enable_firewall = false;
	ret = $[ "workflow_sequence" : `next ];
    }
    else if (chosen_id == "firewall--enable_ssh_in_proposal") {
	y2milestone("SSH enabled by single-click");
	SuSEFirewall::open_ssh_port = true;
	ret = $[ "workflow_sequence" : `next ];
    }
    else if (chosen_id == "firewall--disable_ssh_in_proposal") {
	y2milestone("SSH disabled by single-click");
	SuSEFirewall::open_ssh_port = false;
	ret = $[ "workflow_sequence" : `next ];
    }
    else {
	// user wants to change the firewall settings
	map stored = SuSEFirewall::Export();
	symbol result = (symbol) WFM::CallFunction("firewall", [.propose]);
	if(result != `next)	SuSEFirewall::Import(stored);
	else SuSEFirewall::proposal_changed_by_user = true;
	y2debug("stored=%1", stored);
	y2debug("result=%1", result);
	ret = $[ "workflow_sequence" : result ];
    }
}
/* create titles */
else if(func == "Description") {
    ret = $[
	/* RichText label */
	"rich_text_title" : _("Firewall"),
	/* Menu label */
	"menu_title" : _("&Firewall"),
	"id" : "firewall",
    ];
}
/* write the proposal */
else if(func == "Write") {
    SuSEFirewall::Write();
}
/* unknown function */
else {
    y2error("unknown function: %1", func);
}

/* Finish */
y2debug("ret=%1",ret);
y2milestone("Firewall proposal finished");
y2milestone("----------------------------------------");
return ret;

/* EOF */
}
